import java.security.DigestInputStream
import java.security.MessageDigest

apply plugin: 'com.android.application'
apply from: 'release.gradle'

android {
    compileSdkVersion 30
    ndkVersion '22.0.7026061'

    dependencies {
        implementation "androidx.annotation:annotation:1.1.0"
        implementation "androidx.drawerlayout:drawerlayout:1.1.1"
        implementation "androidx.preference:preference:1.1.1"
        implementation "androidx.viewpager:viewpager:1.0.0"
    }

    defaultConfig {
        applicationId "alpine.term"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1600
        versionName "16.0"

        archivesBaseName = "alpine-term-v$versionName"

        externalNativeBuild {
            ndkBuild {
                cFlags "-std=c11", "-Wall", "-Wextra", "-Werror", "-Os", "-fno-stack-protector", "-Wl,--gc-sections"
            }
        }

        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        ndkBuild {
            path "src/main/jni/Android.mk"
        }
    }
}

def downloadFile(String remoteUrl, String localFile, String expectedChecksum) {
    def digest = MessageDigest.getInstance("SHA-256")

    def localUrl = "src/main/assets/vm_data/" + localFile
    def file = new File(projectDir, localUrl)
    if (file.exists()) {
        def buffer = new byte[16384]
        def input = new FileInputStream(file)
        while (true) {
            def readBytes = input.read(buffer)
            if (readBytes < 0) break
            digest.update(buffer, 0, readBytes)
        }
        def checksum = new BigInteger(1, digest.digest()).toString(16)
        if (checksum == expectedChecksum) {
            return
        } else {
            logger.quiet("Deleting old local file with wrong hash: " + localUrl)
            file.delete()
        }
    }

    logger.quiet("Downloading " + remoteUrl + " ...")

    file.parentFile.mkdirs()
    def out = new BufferedOutputStream(new FileOutputStream(file))

    def connection = new URL(remoteUrl).openConnection()
    connection.setInstanceFollowRedirects(true)
    def digestStream = new DigestInputStream(connection.inputStream, digest)
    out << digestStream
    out.close()

    def checksum = new BigInteger(1, digest.digest()).toString(16)
    if (checksum != expectedChecksum) {
        file.delete()
        throw new GradleException("Wrong checksum for " + remoteUrl + ": expected: " + expectedChecksum + ", actual: " + checksum)
    }
}

clean {
    doLast {
        def tree = fileTree(new File(projectDir, 'src/main/assets'))
        tree.include 'alpine-linux-cdrom.iso'
        tree.include 'alpine-linux-hdd.qcow2'
        tree.each { it.delete() }
    }
}

task getSystemImage(){
    doLast {
        downloadFile("https://dl-cdn.alpinelinux.org/alpine/v3.13/releases/x86_64/alpine-virt-3.13.0-x86_64.iso",
            "alpine-linux-cdrom.iso",
            "daa45a8c12b8458296188d64e6091da79087d47a914d90a0976f6ef92fafba28")
        downloadFile("https://github.com/xeffyr/alpine-term/releases/download/v16.0/alpine-term-hdd-image-2020.01.28.qcow2",
            "alpine-linux-hdd.qcow2",
            "b6500a6640bd152502de8479ad12961fa6afe69b8eb78db0a97fea26d5b8ed7")
    }
}

afterEvaluate {
    android.applicationVariants.all { variant ->
        variant.javaCompileProvider.get().dependsOn(getSystemImage)
    }
}
